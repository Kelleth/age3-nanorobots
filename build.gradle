apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'maven-publish'

group = 'org.age'
sourceCompatibility = 1.8
version = '0.1'

mainClassName = 'org.age.node.Bootstrapper'

repositories {
    mavenCentral()
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    markdownDoclet
}

test {
    useTestNG()
    options {
        listeners << 'org.uncommons.reportng.HTMLReporter'
        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    }
    ext.useDefaultListeners = true
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

//allprojects {
//    tasks.withType(JavaCompile) {
//        options.incremental = true
//    }
//}

dependencies {
    testCompile 'org.testng:testng:6.8.8'
    testCompile 'org.mockito:mockito-all:1.10.8'
    testCompile 'org.uncommons:reportng:1.1.4'
    testCompile 'com.google.inject:guice:4.0-beta5'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile 'org.assertj:assertj-guava:1.3.0'

    compile 'javax.inject:javax.inject:1'
    compile 'org.springframework:spring-context:4.1.2.RELEASE'
    compile('com.hazelcast:hazelcast:3.4-EA') {
        // Not needed and would introduce a lot of duplicated annotations
        exclude group: 'net.sourceforge.findbugs'
    }
    compile 'com.hazelcast:hazelcast-spring:3.4-EA'
    compile 'org.checkerframework:checker-qual:1.8.8'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.slf4j:jcl-over-slf4j:1.7.7'
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'jline:jline:2.12'
    compile 'org.jgrapht:jgrapht-core:0.9.0'
    compile 'com.beust:jcommander:1.35'

    runtime 'org.codehaus.groovy:groovy-all:2.4.0-beta-4'

    markdownDoclet 'ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.2.1"
}

task(console, dependsOn: 'classes') << {
    def classpath = sourceSets.main.runtimeClasspath + configurations.runtime

    def command = [
            'java',
            '-enableassertions',
            '-cp', classpath.collect().join(System.getProperty('path.separator')),
            '-Dlogback.configurationFile=org/age/console/logback.groovy',
            'org.age.console.ConsoleBootstrapper',
    ]

    def proc = new ProcessBuilder(command)
            .redirectOutput(ProcessBuilder.Redirect.INHERIT)
            .redirectInput(ProcessBuilder.Redirect.INHERIT)
            .redirectError(ProcessBuilder.Redirect.INHERIT)
            .start()

    proc.waitFor()

    if (0 != proc.exitValue()) {
        throw new RuntimeException("console exited with status: ${proc.exitValue()}")
    }
}

task node(type: JavaExec) {
    main = 'org.age.node.Bootstrapper'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs '-enableassertions'
    systemProperty "logback.configurationFile", "org/age/node/logback.groovy"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            if(project.hasProperty( "mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url "https://repository.age.agh.edu.pl/content/repositories/snapshots"
            } else {
                url "https://repository.age.agh.edu.pl/content/repositories/releases"
            }
        }
    }
}

javadoc.options {
    docletpath = configurations.markdownDoclet.files.asType(List)
    doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
    addStringOption("parse-timeout", "10")
}
